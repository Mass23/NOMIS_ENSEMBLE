"""
Author: Susheel Bhanu BUSI
Affiliation: ESB group LCSB UniLU
Date: [2021-01-21]
Run: snakemake -s Snakefile --use-conda --cores 45 -rp
Latest modification:
"""

import os, fnmatch
import glob
import pandas as pd

configfile:"config.yaml"
DATA_DIR=config['data_dir']
RESULTS_DIR=config['results_dir']
SAMPLES=[line.strip() for line in open("kegg_plus_unassigned_list", 'r')]    # if using a sample list instead of putting them in a config file
# SAMPLES=config['samples']
KMER=config['kmer']

###########
rule all:
    input:
#        expand(os.path.join(RESULTS_DIR, "results/{sample}_{kmer}mers.txt"), sample=SAMPLES, kmer=KMER),
#        os.path.join(RESULTS_DIR, "results/edited_kmers/euci_7mer_subset_100000"),
        os.path.join(RESULTS_DIR, "results/concat/concatenated_KEGG.fasta"),
        os.path.join(RESULTS_DIR, "results/edited_kmers/functional_dbscan_output.csv"),
        os.path.join(RESULTS_DIR, "results/mmseqs2/clusters.tsv")

################################
# rules for files and analyses #
################################
rule kmers:
    input:
        fasta=os.path.join(DATA_DIR, "{sample}/{sample}.fasta")
    output:
        kmers=os.path.join(RESULTS_DIR, "results/{sample}_{kmer}mers.txt")
    log:
        os.path.join(RESULTS_DIR, "logs/{sample}.{kmer}mers.log")
    conda:
        os.path.join("envs/khmer.yaml")
    message:
        "Extracting {wildcards.kmer}mers from {wildcards.sample}"
    script:
        "scripts/kmers_khmer.py"

rule filename:
    input:
        rules.kmers.output
    output:
        os.path.join(RESULTS_DIR, "results/edited_kmers/edited_{sample}_{kmer}mers.txt")
    log:
        os.path.join(RESULTS_DIR, "logs/edited_{sample}.{kmer}mers.log")
    message:
        "Adding filename as column and concatenating all files"
    shell:
        "(date && sed 's/^/{wildcards.sample}\t/' {input} > {output} && date)"

rule cat:
    input:
        expand(os.path.join(RESULTS_DIR, "results/edited_kmers/edited_{sample}_{kmer}mers.txt"), sample=SAMPLES, kmer=KMER)
    output:
        os.path.join(RESULTS_DIR, "results/edited_kmers/euci_merged_7mers.txt")
    log:
        os.path.join(RESULTS_DIR, "logs/concatenation.kmers.log")
    message:
        "Concatenating all kmer files to single file"
    shell:
        "(date && cat {input} > {output} && date)"

rule subset:
    input:
        rules.cat.output
    output:
        os.path.join(RESULTS_DIR, "results/edited_kmers/euci_7mer_subset_100000")
    params:
        total=41068843,
        number=100000
    log:
        os.path.join(RESULTS_DIR, "logs/subset.log")
    message:
        "Selecting 100000 random lines from the file for massimo"
    script:
        "scripts/random_subset.py"

rule dbscan:
    input:
        subset=rules.subset.output
    output:
        score=os.path.join(RESULTS_DIR, "results/edited_kmers/functional_dbscan_output.csv")
    conda:
        os.path.join("envs/dbscan.yaml")
    log:
        os.path.join(RESULTS_DIR, "logs/dbscan.log")
    message:
        "Calculating different values of epsilon for clustering"
    script:
        "scripts/9_Gene_clustering_DBSCAN.py"


##############
# Clustering #
##############
rule rename:
    input:
        rules.kmers.input
    output:
        os.path.join(RESULTS_DIR, "results/concat/renamed_{sample}.fasta")
    log:
        os.path.join(RESULTS_DIR, "logs/rename.{sample}.fasta")
    conda:
        os.path.join("envs/bbmap.yaml")
    message:
        "Renaming headers for based on KEGG + Unassigned"
    shell:
         "(date && bbrename.sh in={input} out={output} prefix={wildcards.sample} addprefix=t && date)"

rule fasta:
    input:
        expand(os.path.join(RESULTS_DIR, "results/concat/renamed_{sample}.fasta"), sample=SAMPLES)
    output:
        os.path.join(RESULTS_DIR, "results/concat/concatenated_KEGG.fasta")
    log:
        os.path.join(RESULTS_DIR, "logs/concatenation.fasta.log")
    message:
        "Concatenating KEGG + Unassigned fasta files for clustering"
    shell:
        "(date && cat {input} >> {output} && date)"

rule mmseqs2_db:
    input:
        rules.fasta.output
    output:
        tsv=os.path.join(RESULTS_DIR, "results/mmseqs2/clusters.tsv"),
#        faa=temp(os.path.join(RESULTS_DIR, "results/mmseqs2/proteins.faa")),
        tmpdir=temp(directory(os.path.join(RESULTS_DIR, "results/mmseqs2/tmp")))
    log:
        os.path.join(RESULTS_DIR, "logs/mmseqs2.log")
    threads:
        config["mmseqs2"]["threads"]
    params:
        # basenames (w/ extension) for database and clustering files
        db=os.path.join(RESULTS_DIR, "results/mmseqs2/mmseqs2DB"),
        cl=os.path.join(RESULTS_DIR, "results/mmseqs2/mmseqs2CLUST")
    conda:
        os.path.join("envs/mmseqs2.yaml")
    message:
        "Creating mmseqs2 database"
    shell:
        "(date && "
        # put all proteins together
        # DB, cluster, TSV output
        "mmseqs createdb --dbtype 1 -v 3 {input} {params.db} && "
        "mmseqs linclust --threads {threads} {params.db} {params.cl} {output.tmpdir} && "
        "mmseqs createtsv {params.db} {params.db} {params.cl} {output.tsv} && "
        "date) &> {log}"

